\section{非線形モデル}
	倒立振子の非線形モデルのコードを載せる。下向きを基準としている。
	\begin{breakitembox}[l]{diff\_eqs}\baselineskip=1ex
		\begin{verbatimtab}[4]
Func Matrix diff_eqs(t,x,u)
Real t;
Matrix x,u;
{
	Real r,th,dr,dth;
	Matrix xp,dxp;
	Matrix K,KZ;

	r=x(1,1);
	th=x(2,1);
	dr=x(3,1);
	dth=x(4,1);

	K=[[M + m , m *l*cos(th)]
	   [m*l*cos(th) , J + m*l*l]];

	KZ=[[-f*dr+ m*l*sin(th)*dth*dth + a * u(1,1)]
		[ m*g*l*sin(th) - c*dth ]];
	
	dxp=[[x(3:4,1)][K\KZ]]; // 倒立振子の状態の微分（非線形モデル)

	cnt = cnt+1;
	XX(1,cnt) = dxp(1,1)-Xh(1,1);
	
	// 出力を更新
	return dxp;//Xは６行の行列になる
}
		\end{verbatimtab}
	\end{breakitembox}
%-------------------------------------------------------------------
\section{線形モデル}
	倒立振子の線形モデルのコードを載せる。下向きを基準としている。
	\begin{breakitembox}[l]{diff\_eqs}\baselineskip=1ex
		\begin{verbatimtab}[4]
Func void diff_eqs(DX,t,X,UY)
Real t;
Matrix X,DX,UY;
{
	Real M,m,l,J,f,a,c,g;
	Matrix xp,up,dxp;
	Matrix A,B,A21,A22,B2,K;
	
	//物理パラメータの設定
	M=1.49;   m=0.038; l=0.13;
	J=4.5E-4; f=15.10; a=0.73;
	c=2.1E-4; g=9.8;
	
	K=[[M+m,m*l][m*l,J+m*l^2]];
	A21 = K~*[[0,0][0,m*g*l]];
	A22 = K~*[[-f,0][0,-c]];
	A=[[Z(2),I(2)][A21,A22]];
	
	B2 = K~*[[a][0]];
	B=[[Z(2,1)][B2]];
	
	xp = X;
	up = UY;
	
	dxp = A*xp+B*up;
	
	DX = [dxp];

}
		\end{verbatimtab}
	\end{breakitembox}
%-------------------------------------------------------------------
\section{システム解析}
	第4章4.1節のシステム解析に用いたコードを載せる。
	ただし、行列A,B,Cについては別の部分で計算している。
	\begin{breakitembox}[l]{system\_analysis}\baselineskip=1ex
		\begin{verbatimtab}[4]
//システムの極(Aの固有値)を計算
D=eigval(A);

//可制御性行列のランクを計算し、可制御性を調べる
Nc=[B,A*B,A*A*B,A*A*A*B];

//可観測性行列もランクを計算し、可観測性を調べる。
No=[[C][C*A][C*A*A][C*A*A*A]];
		\end{verbatimtab}
	\end{breakitembox}
%-------------------------------------------------------------------
\section{状態フィードバック}
	状態フィードバックを設計算するのに用いたコードを載せる。ただし、LQ最適制御に基づくフィードバック則とする。
	\begin{breakitembox}[l]{lqr}\baselineskip=1ex
		\begin{verbatimtab}[4]
Matrix A, B, Q, R, F, P;

Q = diag(1, 1, 1, 1);
R = [1];
{F,P} = lqr(A, B, Q, R);
		\end{verbatimtab}
	\end{breakitembox}
%-------------------------------------------------------------------
\section{最小次元オブザーバ}
	最小次元オブザーバをゴピナスの方法で計算するのに用いたコードを載せる。
	\begin{breakitembox}[l]{obsg}\baselineskip=1ex
		\begin{verbatimtab}[4]
CoMatrix obs_p;

obs_p = trans([(-2,0), (-2,0)]);
{Ah, Bh, Ch, Dh, Jh} = obsg(A, B, C, obs_p);
		\end{verbatimtab}
	\end{breakitembox}
%-------------------------------------------------------------------	
\section{コントローラの離散化}
	連続時間オブザーバを離散化するたために用いたコードを載せる。
	\begin{breakitembox}[l]{c2d}\baselineskip=1ex
		\begin{verbatimtab}[4]
// ここは関数の先頭
Real dt;
Matrix Ah,Bh,Jh;
Matrix Ahd,Bhd,Jhd,Hhd;

//ここはオブザーバの設計の後
dt = 0.005;
{Ahd,Hhd} = c2d(Ah, [Bh Jh], dt);
Bhd = Hhd(:,1:2);
Jhd = Hhd(:,3);
		\end{verbatimtab}
	\end{breakitembox}
%-------------------------------------------------------------------
\section{シミュレーション}
	第5章でシミュレーションを行ったコードを載せる。
	\begin{breakitembox}[l]{InPeAboveNonLinerDesign3.mm}\baselineskip=1ex
		\begin{verbatimtab}[4]
Matrix A; //システム行列
Matrix B; //入力行列
Matrix C; //出力行列
Matrix F; //状態フィードバック行列
Matrix z; // オブザーバの状態
Matrix Ah,Bh,Ch,Dh,Jh; //オブザーバに関する行列
Matrix Ahd,Bhd,Jhd;
Matrix Xh,XX,Xtest; //推定値
Real M,m,l,J,f,a,c,g,c1,c2; //パラメータ

Func void main()
{
	Real t0,t1,r0,th0,tol;
	Real dt,dtsav; //離散化に用いる変数
	Matrix x0,z0,xp0,TC,XC,UC;
	void calcParameter();
	Matrix diff_eqs(),link_eqs();
	
	t0 = 0.0; //シミュレーション開始時刻
	t1 = 18.0; //シミュレーション終了時刻
	r0 = 0; //位置の初期値
	th0 = 10.0; //角度の初期値
	x0 = [r0, th0/180*PI ,0 ,0]'; //倒立振子の初期状態'
	z = [0,0]'; //オブザーバーの初期状態'
	dt = 0.005; //サンプリング周期
	tol=1.0E-9;//許容誤差
	dtsav = 0.05; //データ保存間隔
	
	print "Now simulating\n";
	
	cnt=0; // 初期化
    calcParameter(); //シミュレーションに必要なパラメータを計算
	// Ode()によってシミュレーションを行う
	{TC,XC,UC} =
	 Ode45HybridAuto(t0,t1,dt,x0,diff_eqs,link_eqs,tol,dtsav);
	// diff_eqs() は微分方程式を記述する関数
	// link_eqs() は複数の微分方程式の関係を記述する関数。
	// TC:時間の時系列
	// XC:状態x(t)の時系列
	// UC:入出力ベクトルの時系列
	
	//数値回を表示する。横軸t, 縦軸x(t)
	mgreplot(1,TC,XC(1,*),{"r"});
	mgreplot(2,TC,XC(2,*),{"Theta"});
	
	print TC >> "TC.mat";
	print XC >> "XC.mat";
	print UC >> "UC.mat";
}



// diff_eqs() は微分方程式を記述する関数
Func Matrix diff_eqs(t,x,u)
// tは時間
Real t;
Matrix x,u;
{
	Real r,th,dr,dth;
	Matrix xp,dxp;
	Matrix K,KZ;
	
	r=x(1,1);
	th=x(2,1);
	dr=x(3,1);
	dth=x(4,1);

	K=[[M + m , m *l*cos(th)]
	   [m*l*cos(th) , J + m*l*l]];

	KZ=[[-f*dr+ m*l*sin(th)*dth*dth + a * u(1,1)]
		[ m*g*l*sin(th) - c*dth ]];
	
	dxp=[[x(3:4,1)][K\KZ]]; // 倒立振子の状態の微分（非線形モデル)
	
	cnt = cnt+1;
	XX(1,cnt) = dxp(1,1)-Xh(1,1);

    // 出力を更新
	return dxp;//Xは６行の行列になる
}



// link_eqs() は複数の微分方程式の関係を記述する関数。
Func Matrix link_eqs(t,x)
Real t;
Matrix x;
{
    Matrix u;
    Matrix xref;
	Matrix xh,y;

    //台車の可動範囲に関する制限
    if (x(1,1) <= -0.16 || 0.16 <= x(1,1)) { // r=x(1,1)
    	OdeStop();
    }
	
	y = C*x; // 出力の計算
	xh = Ch*z + Dh*y; // 状態の推定値
	Xh= xh; //推定値を保存
	xref = [0,0,0,0]'; // 状態の目標値'
	if(0<=t && t<=5){
		xref = [0,0,0,0]'; // 状態の目標値'
	}else if(5<t&&t<=10){
		xref = [0.1,0,0,0]'; // '
	}else if(10<t&&t<=15){
		xref = [0,0,0,0]'; // '
	}
    u = F*(xref - xh);

    //入力の大きさに関する制限
    if(u(1,1) <= -15) {
        u(1,1) = -15;
    }else if (u(1,1) >= 15) {
        u(1,1) = 15;
    }

	z = Ahd*z + Bhd*y + Jhd*u; // オブザーバの状態更新
    
    //入力を更新
    return u;
}

Func void calcParameter(){

    CoMatrix pc,obs_p;
    Matrix A21,A22,B2,K,N;
	Matrix Ah ,Bh ,Jh ; // 連続時間オブザーバの係数行列
	Matrix Hhd; // 離散時間オブザーバの係数行列
	Real dt; //サンプリング周期
	Matrix Q,R,P;

    

    //物理パラメータの設定
	M=0.69;   m=0.031; l=0.15;
	J=2.5E-4; f=7.6; a=0.61;
	c=5.4E-5; g=9.8; c1=1.0;
	c2=1.0;

    // システム行列Aの準備
    K=[[M+m,m*l][m*l,J+m*l^2]];
	A21 = K~*[[0,0][0,m*g*l]];
	A22 = K~*[[-f,0][0,-c]];
	A=[[Z(2),I(2)][A21,A22]];

    //入力行列Bの準備
	B2 = K~*[[a][0]];
	B=[[Z(2,1)][B2]];

	//出力行列Cの準備
    N=[[c1,0][0,c2]];
    C=[N,Z(2)];

	//状態フィードバック行列Fを準備する
    //pc=[(-100,0),(-50,0),(-1,0),(-1,0)]'; 閉ループ系の極'
    //F = pplace(A,B,pc);
	
	//LQ最適制御のための状態フィードバック行列Ｆの準備
	Q = diag(1E5,1E5,1,1);
	R = [1];
	{F,P} = lqr(A, B, Q, R);

	//ゴピナスの方法で最初次元観測器を設計する
	obs_p = trans([(-30,0), (-30,0)]); // オブザーバの極
	//read obs_p; // 極を編集する
	{Ah, Bh, Ch, Dh, Jh} = obsg(A, B, C, obs_p); // ゴピナスの方法による設計

	// 連続時間オブザーバをサンプリング周期で離散化
	dt = 0.005;
	{Ahd,Hhd} = c2d(Ah,[Bh, Jh], dt); //離散化
	Bhd = Hhd(:,1:2); //係数行列の取り出し
	Jhd = Hhd(:,3); // 係数行列の取り出し
}
		\end{verbatimtab}
	\end{breakitembox}	
%-------------------------------------------------------------------
\section{安定化制御及び目標値変更実験}
	安定化制御及び目標値変更実験を行うために使用したコードを載せる。
	\begin{breakitembox}[l]{sample.mm}\baselineskip=1ex
		\begin{verbatimtab}[4]
#define LOGMAX 10000

Integer cmd, count;
Real smtime;
Matrix u, y;
Array data;
Matrix Ahd,Bhd,Chd,Dhd,Jhd,F;
Matrix z;
Integer qrr,qth,oprh,opthh;
Real ref;

// センサとアクチュエータ関連の変数 (hardware.mmで使用される)
Matrix mp_data, PtoMR;

// メイン関数
Func void main()
{
	void para_init(), var_init();
	void on_task(), break_task(), off_task_loop();
	void machine_ready(), machine_stop(), data_save();
	void calcParameter();

	para_init();			// パラメータの初期化
	var_init();             // 変数の初期化
	machine_ready();        // 実験装置の準備
	calcParameter();

	rtSetClock(smtime);     // サンプリング周期の設定
	rtSetTask(on_task);		// オンライン関数の設定(制御)
	rtSetBreak(break_task); // 割り込みキーに対応する関数の設定

	rtStart();              // リアルタイム制御開始
	off_task_loop();        // オフライン関数
	rtStop();               // リアルタイム制御終了

	machine_stop();         // 実験装置を停止

	data_save();			// データを保存する
}

// パラメータの初期化
Func void para_init()
{
}

// 変数の初期化
Func void var_init()
{
	smtime = 0.01;		// サンプリング周期 [s]
	cmd = 0;			// 制御出力を抑制
	count = 0;			// ロギングデータの数
	data = Z(4,LOGMAX); // ロギングデータを保存する場所
	z = [0 0]';            // オブザーバの初期値
	qrr = 0;
	qth =0;
	oprh = 0;
	opthh = 0;
ref = -0;
}

// オンライン関数
Func void on_task()
{
	Matrix xh,xref;
	Matrix sensor();
	void actuator();

	y = sensor();				// センサから入力

	xh = Chd*z + Dhd*y; // 状態の推定値
	if(count * smtime< 5){
		ref = 0;
	} else if(count * smtime < 10){
		ref = 0.1;
	} else if (count * smtime < 15){
		ref = 0;
	}

	xref = [ref 0 0 0]'; //状態の目標値'
		
	u = F*(xref - xh); // 制御入力
	z = Ahd*z + Bhd*y + Jhd*u; // オブザーバの状態更新

	// リハーサル中でなければ
	if (cmd == 1 && ! rtIsRehearsal()) {
		actuator(u(1));	 		// アクチュエータへ出力
	}

	// データのロギング
	if (cmd == 1 && count < LOGMAX) {
		count++;
		data(1:1, count) = u; // 入力
		data(2:3, count) = y; // 実測値（台車一、振り子角度）
		data(4, count)  = ref;
	}
}

// オフライン関数
Func void off_task_loop()
{
	Integer end_flag;

	end_flag = 0;

	gotoxy(5, 6);
	printf("'c': アクチュエータへ出力");
	gotoxy(5, 7);
	printf("ESC: アクチュエータへ出力停止");
	gotoxy(5, 8);
	printf("r:目標値の変更! ");

	do {
		gotoxy(5, 11);
		printf("  台車位置 = %8.4f[m], 振子角度 = %8.4f[deg]",
			y(1), y(2)/PI*180);
		gotoxy(5, 12);
		printf("入力 = %10.4f[N]", u(1));

		gotoxy(5, 14);
		printf("データ数 = %4d, 
				時間 = %7.3f [s] ref = %4f", count, count*smtime,ref);
		if (rtIsTimeOut()) {
			gotoxy(5, 18);
			warning("\n時間切れ !\n");
			break;
		}

		if (kbhit()) {
			switch (getch()) {
			  case 0x1b:            /* ESC */
				end_flag = 1;
				break;
		/* 'c' */  case 0x43: // アクチュエータへ出力開始
		/* 'C' */  case 0x63: // If 'c' or 'C' is 
				      // pressed, start motor
				cmd = 1;
				break;
			   case 0x72:
			   	gotoxy(5,16);
				print "台車の目標値「m」:"; read ref;
				gotoxy(5,16);
				printf("  				");
				break;
			  default:
				break;
			}
		}
    } while ( ! end_flag);  // If end_flag != 0, END
}

// 割り込みキーに対応する関数
Func void break_task()
{
	void machine_stop();

	rtStop();
	machine_stop(); // 実験装置停止
}

// 実験装置の準備
Func void machine_ready()
{
	void sensor_init(), actuator_init();

	sensor_init();                  // センサの初期化
	actuator_init();                // アクチュエータの初期化

	gotoxy(5,5);
	printf("台車の初期位置 : レールの中央");
	gotoxy(5,6);
	printf("振子の初期位置 : 真下");
	gotoxy(5,9);
	pause "台車と振子を初期位置に移動し，リターンキーを入力して下さい。";
	gotoxy(5,9);
	printf("                                         ");
	gotoxy(5,9);
	pause "リターンキーを入力すると，制御が開始されます。";
	clear;
}

// 実験装置停止
Func void machine_stop()
{
	void actuator_stop();

	actuator_stop();
}

// データのファイルへの保存
Func void data_save()
{
	String filename;
	Array TT;

	filename = "experiment_Q56obs60dt10";
	read filename;

	if (count > 1) {
		TT = [0:count-1]*smtime;
		print [[TT][data(:,1:count)]] >> filename +".mat";
	}
}

Func void calcParameter(){

    CoMatrix pc,obs_p;
    Matrix A21,A22,B2,K,N;
	Matrix Ah ,Bh ,Jh ; // 連続時間オブザーバの係数行列
	Matrix Hhd; // 離散時間オブザーバの係数行列
	Real dt; //サンプリング周期
	Real M,m,l,J,f,a,c,g,c1,c2; //パラメータ
	Matrix A; //システム行列
	Matrix B; //入力行列
	Matrix C; //出力行列
	Matrix Q,R,P;

    
	// パラメータの調整
	qrr = 100000;
	qth = 1000000;
	oprh = -60;
	opthh = -60;

    //物理パラメータの設定
	M=0.69;   m=0.031; l=0.15;
	J=2.5E-4; f=7.6; a=0.61;
	c=5.4E-5; g=9.8; c1=1.0;
	c2=1.0;

    // システム行列Aの準備
    K=[[M+m,m*l][m*l,J+m*l^2]];
	A21 = K~*[[0,0][0,m*g*l]];
	A22 = K~*[[-f,0][0,-c]];
	A=[[Z(2),I(2)][A21,A22]];

    //入力行列Bの準備
	B2 = K~*[[a][0]];
	B=[[Z(2,1)][B2]];

	//出力行列Cの準備
    N=[[c1,0][0,c2]];
    C=[N,Z(2)];

	//状態フィードバック行列Fを準備する(極配置)
    /*pc=[(-100,0),(-50,0),(-2,0),(-3,0)]'; //閉ループ系の極'
    F = pplace(A,B,pc);*/

    //状態フィードバック行列Fを準備する（LQ最適制御)
    Q = diag(qrr,qth,1,1); // 対角行列
    R = [1];
    {F,P} = lqr(A,B,Q,R); //P:リカッティ方程式の解

	//ゴピナスの方法で最初次元観測器を設計する
	obs_p = trans([(oprh,0), (opthh,0)]); // オブザーバの極
	//read obs_p; // 極を編集する
	{Ah, Bh, Chd, Dhd, Jh} = obsg(A, B, C, obs_p); // ゴピナスの方法による設計

	// 連続時間オブザーバをサンプリング周期で離散化
	dt = smtime;
	{Ahd,Hhd} = c2d(Ah,[Bh, Jh], dt); //離散化
	Bhd = Hhd(:,1:2); //係数行列の取り出し
	Jhd = Hhd(:,3); // 係数行列の取り出し
print "aa" >> "a.txt";

}

		\end{verbatimtab}
	\end{breakitembox}
%-------------------------------------------------------------------
\section{振り上げ制御及び安定化実験}
	振り上げ制御及び安定化実験を行うために使用したコードを載せる。
	\begin{breakitembox}[l]{sample.mm}\baselineskip=1ex
		\begin{verbatimtab}[4]
#define LOGMAX 10000





Integer cmd, count;
Real smtime;
Matrix u, y;
Array data;
Matrix Aod,Bod,Cod,Dod,F;
Matrix z,yw;
Matrix zde,zdo;
Integer qrr,qth,oprh,opthh;
Integer swinging,cnt;
Real M,m,l,J,f,a,c,g,c1,c2; //パラメータ
Real pre_r,pre_th,pre_dr,pre_dth,pre_ddth;

// センサとアクチュエータ関連の変数 (hardware.mmで使用される)
Matrix mp_data, PtoMR;

// メイン関数
Func void main()
{
	void para_init(), var_init();
	void on_task(), break_task(), off_task_loop();
	void machine_ready(), machine_stop(), data_save();
	void calcParameter();

	para_init();			// パラメータの初期化
	var_init();             // 変数の初期化
	machine_ready();        // 実験装置の準備
	calcParameter();

	rtSetClock(smtime);     // サンプリング周期の設定
	rtSetTask(on_task);		// オンライン関数の設定(制御)
	rtSetBreak(break_task); // 割り込みキーに対応する関数の設定

	rtStart();              // リアルタイム制御開始
	off_task_loop();        // オフライン関数
	rtStop();               // リアルタイム制御終了

	machine_stop();         // 実験装置を停止

	data_save();			// データを保存する
}

// パラメータの初期化
Func void para_init()
{
	zde = [0,PI]'; //'
	zdo = [0,0]';//'
	swinging = 1;

	//物理パラメータの設定
	M=0.69;   m=0.031; l=0.15;
	J=2.5E-4; f=7.6; a=0.61;
	c=5.4E-5; g=9.8; c1=1.0;
	c2=1.0;

	pre_r = 0;
	pre_th = PI;
	pre_dr = 0;
	pre_dth = 0;
	pre_ddth = 0;  
	
	//ここでuを初期化すればよいのではないか
}

// 変数の初期化
Func void var_init()
{
	Matrix Swinging();

	smtime = 0.005;		// サンプリング周期 [s]
	cmd = 0;			// 制御出力を抑制
	count = 0;			// ロギングデータの数
	data = Z(3,LOGMAX); // ロギングデータを保存する場所
	z = [0 0]';            // オブザーバの初期値'
	qrr = 0;
	qth =0;
	oprh = 0;
	opthh = 0;
	count = 0;
	u = Swinging([[0][-PI+0.01][0][0][0][0][0][0]]);
}

// オンライン関数
Func void on_task()
{
	Matrix xh,xref;
	Matrix xhe,xho;
	Matrix sensor(),AngleWrapper(),DiscreteEstimator()
						,DiscreteObserver(),Swinging();
	void actuator();

	y = sensor();				// センサから入力

	//AngleWrapper 入力：ｙ　出力：ｙｗ
    	yw = AngleWrapper(y);

	// DiscreteEstimator 入力：yw 出力:xhe
	xhe = DiscreteEstimator(yw);

	// DiscreteObserver 入力：[[u][yw]]　出力：xho
	xho = DiscreteObserver([[u][yw]]);

	// Swinging 入力:[[xhe][xho]] 出力：u
	u = Swinging([[xhe][xho]]);

	// リハーサル中でなければ
	if (cmd == 1 && ! rtIsRehearsal()) {
		actuator(u(1));	 		// アクチュエータへ出力
	}

	// データのロギング
	if (cmd == 1 && count < LOGMAX) {
		count++;
		data(1:1, count) = u; // 入力
		data(2:3, count) = y; // 実測値（台車一、振り子角度）
	}
}

// オフライン関数
Func void off_task_loop()
{
	Integer end_flag;

	end_flag = 0;

	gotoxy(5, 6);
	printf("'c': アクチュエータへ出力");
	gotoxy(5, 7);
	printf("ESC: アクチュエータへ出力停止");

	do {
		gotoxy(5, 11);
		printf("  台車位置 = %8.4f[m], 振子角度 = %8.4f[deg]",
			y(1), yw(2)/PI*180);
		gotoxy(5, 12);
		printf("入力 = %10.4f[N]", u(1));

		gotoxy(5, 14);
		printf("データ数 = %4d, 時間 = %7.3f [s]", count, count*smtime);
		if (rtIsTimeOut()) {
			gotoxy(5, 18);
			warning("\n時間切れ !\n");
			break;
		}

		if (kbhit()) {
			switch (getch()) {
			  case 0x1b:            /* ESC */
				end_flag = 1;
				break;
		/* 'c' */  case 0x43: // アクチュエータへ出力開始
		/* 'C' */  case 0x63: // If 'c' or 'C' is 
				      // pressed, start motor
				cmd = 1;
				break;
			  default:
				break;
			}
		}
    } while ( ! end_flag);  // If end_flag != 0, END
}

// 割り込みキーに対応する関数
Func void break_task()
{
	void machine_stop();

	rtStop();
	machine_stop(); // 実験装置停止
}

// 実験装置の準備
Func void machine_ready()
{
	void sensor_init(), actuator_init();

	sensor_init();                  // センサの初期化
	actuator_init();                // アクチュエータの初期化

	gotoxy(5,5);
	printf("台車の初期位置 : レールの中央");
	gotoxy(5,6);
	printf("振子の初期位置 : 真下");
	gotoxy(5,9);
	pause "台車と振子を初期位置に移動し，リターンキーを入力して下さい。";
	gotoxy(5,9);
	printf("                                           ");
	gotoxy(5,9);
	pause "リターンキーを入力すると，制御が開始されます。";
	clear;
}

// 実験装置停止
Func void machine_stop()
{
	void actuator_stop();

	actuator_stop();
}

// データのファイルへの保存
Func void data_save()
{
	String filename;
	Array TT;

	filename = "huriage";
	read filename;

	if (count > 1) {
		TT = [0:count-1]*smtime;
		print [[TT][data(:,1:count)]] >> filename +".mat";
	}
}

Func void calcParameter(){

    CoMatrix pc,obs_p;
    Matrix A21,A22,B2,K,N;
	Matrix Ah ,Bh ,Jh ; // 連続時間オブザーバの係数行列
	Matrix Hhd; // 離散時間オブザーバの係数行列
	Real dt; //サンプリング周期
	Matrix A; //システム行列
	Matrix B; //入力行列
	Matrix C; //出力行列
	Matrix Q,R,P;
	Matrix Chd,Dhd;

    
	// パラメータの調整
	qrr = 100000;
	qth = 100000;
	oprh = -30;
	opthh = -30;

    // システム行列Aの準備
    K=[[M+m,m*l][m*l,J+m*l^2]];
	A21 = K~*[[0,0][0,m*g*l]];
	A22 = K~*[[-f,0][0,-c]];
	A=[[Z(2),I(2)][A21,A22]];

    //入力行列Bの準備
	B2 = K~*[[a][0]];
	B=[[Z(2,1)][B2]];

	//出力行列Cの準備
    N=[[c1,0][0,c2]];
    C=[N,Z(2)];

	//状態フィードバック行列Fを準備する(極配置)
    /*pc=[(-100,0),(-50,0),(-2,0),(-3,0)]'; //閉ループ系の極'
    F = pplace(A,B,pc);*/

    //状態フィードバック行列Fを準備する（LQ最適制御)
    Q = diag(qrr,qth,1,1); // 対角行列
    R = [1];
    {F,P} = lqr(A,B,Q,R); //P:リカッティ方程式の解

	//ゴピナスの方法で最初次元観測器を設計する
	obs_p = trans([(oprh,0), (opthh,0)]); // オブザーバの極
	//read obs_p; // 極を編集する
	{Ah, Bh, Chd, Dhd, Jh} = obsg(A, B, C, obs_p); // ゴピナスの方法による設計

	// 連続時間オブザーバをサンプリング周期で離散化
	dt = 0.005;
	{Aod,Bod} = c2d(Ah, [Jh Bh], dt); //離散化
	Cod = Chd; //係数行列の取り出し
	Dod = [Z(Rows(Dhd),Cols(Jh)),Dhd]; // 係数行列の取り出し
}

Func Matrix AngleWrapper(y)
Matrix y;
{
	Real r,th;
	Matrix yw;

	//入力：ｙ　出力：ｙｗ
    r = y(1,1);
    th = y(2,1);
    while (th < -PI) {
      th = th + 2*PI;
    }
    
    while (th > PI) {
      th = th - 2*PI;
    }
        

	
    yw = [[r][th]];

	return yw;
}

Func Matrix DiscreteEstimator(yw)
Matrix yw;
{
	Matrix xhe;

	xhe = [[yw][(yw-zde)/smtime]];
	zde = yw; //更新
	return xhe;
}

Func Matrix DiscreteObserver(uyw)
Matrix uyw;
{
	Matrix xho;
	Matrix r_th;

	r_th = uyw(2:3,1);
	if(swinging == 1) {
		xho = [[r_th][Z(2,1)]];
	} else {
		xho = Cod*zdo + Dod*uyw;
	}

	if(swinging == 1) { // 更新
		zdo = [0,0]'; //'
	} else {
		zdo = Aod*zdo + Bod*uyw;
	}

	return xho;

}

Func Matrix Swinging(xheo)
Matrix xheo;
{
	Real r,th,dr,dth,ddr,ddth;
	Real E0,r_max,th_min;
	Real E;
	Real n,kk;
	Integer isSolverTrial();

	n = 0.4;
	kk = 100000;

	E0 = 0;
	r_max = 0.07;
	th_min = 12.0;

	r = xheo(1,1);
	th = xheo(2,1);
	dr = xheo(3,1);
	dth = xheo(4,1);

	if(isSolverTrial() == 0 
			&& abs(th) <= th_min*PI/180 && abs(r) < r_max) {
		swinging = 0;
	}
	if(isSolverTrial() == 0 && abs(th) > th_min*PI/180){
		swinging = 1;
	}

	if(swinging == 1){ // 振り上げ制御
		E = (J + m*l*l)*dth*dth/2 + m*g*l*(cos(th) - 1);
		ddr = max(-n*g,min(n*g,kk*(E-E0)*sgn(dth*cos(th))));

		if(isSolverTrial() == 0){
			pre_ddth = ddth = (dth - pre_dth)/smtime;
			pre_dth = dth;
		} else {
			ddth = pre_ddth;
		}

		u = [((M + m)*ddr + m*l*(ddth*cos(th) 
				- dth*dth*sin(th)) + f*dr)/a];

		if(abs(r) > r_max && r*u(1,1) > 0) {
			u = [0];
		}
	} else { // 安定化制御
		u = -F * xheo(5:8,1);
	}
	return u;
}

Func Integer isSolverTrial(){
	return 0;
}

		\end{verbatimtab}
	\end{breakitembox}
%-------------------------------------------------------------------
